(in-package #:quest)

(declaim (optimize (debug 3)))

(defdao quest ()
    ((id :col-type serial :reader id)
     (title :col-type (or text db-null) :initarg :title :reader title)
     (user-id :col-type (or integer db-null) :initarg :user-id :accessor user-id)
     (chapter-count :col-type integer :col-default 0 :reader chapter-count)
     (created :col-type :timestamp-with-time-zone :col-default (:now) :reader created))
  (:keys id)
  (:foreign-key user user-id id))

(defdao chapter ()
    ((id :col-type serial :reader id)
     (quest-id :col-type integer :initarg :quest-id :reader quest-id)
     (title :col-type (or text db-null) :initarg :title :accessor title)
     (ordinal :col-type integer :accessor ordinal)
     (update-count :col-type integer :col-default 0 :reader update-count)
     (created :col-type :timestamp-with-time-zone :col-default (:now) :reader created))
  (:keys id)
  (:unique ordinal quest-id)
  (:foreign-key quest quest-id id))

(defprepared-with-names alloc-chapter-ordinal (quest-id)
    ((:update 'quest :set 'chapter-count (:+ 1 'chapter-count)
              :where (:= 'id '$1)
              :returning 'chapter-count)
     quest-id)
    :single)

(defmethod insert-dao :around ((chapter chapter))
  (with-transaction (insert-chapter)
    (setf (slot-value chapter 'ordinal)
          (alloc-chapter-ordinal (quest-id chapter)))
    (call-next-method)))

(defprepared-with-names chapters (quest)
    ((:order-by (:select :* :from 'chapter :where (:= 'quest-id :$1))
                (:desc 'ordinal))
     (id quest))
    (:dao chapter))

(defprepared-with-names latest-chapter (quest)
    ((:limit (:order-by (:select :* :from 'chapter :where (:= 'quest-id :$1))
                        (:desc 'ordinal))
             1)
     (id quest))
    (:dao chapter :single))